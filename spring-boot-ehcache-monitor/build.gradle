plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8

configurations {
    javadocDeps
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    classpath += configurations.javadocDeps
    options.addStringOption("sourcepath", "")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'spring-boot-ehcache-monitor'
            version project.version
            artifact javadocJar
            artifact sourcesJar

            pom.withXml {
                def node = asNode()
                node.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Spring Boot Ehcache Monitor'
                    description 'Ehcache Monitoring Web for Srping Boot Application.'
                    url 'https://github.com/AntonioCappiello/date-utils'

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'kingbbode'
                            name 'YongGeun Kwon'
                            email 'kingbbode@gmail.com'
                        }
                    }
                    scm {
                        connection 'https://github.com/kingbbode/spring-boot-ehcache-monitor.git'
                        developerConnection 'https://github.com/kingbbode/spring-boot-ehcache-monitor.git'
                        url 'https://github.com/kingbbode/spring-boot-ehcache-monitor'
                    }
                }
            }
        }
    }
}

bintray {
    user = 'kingbbode'
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'springboot'
        name = 'spring-boot-ehcache-monitor'
        version {
            name = project.version
        }
    }
}

dependencies {
    compile('com.vaadin:vaadin-spring-boot-starter')
    compile('com.byteowls:vaadin-chartjs:1.0.0')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('net.sf.ehcache:ehcache')
    javadocDeps 'javax.portlet:portlet-api:2.0'
    compileOnly("org.projectlombok:lombok")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:8.1.0"
    }
}